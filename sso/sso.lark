start: statement*

?statement: (assignment | expr | if_stmt | for_stmt) (";" | NEWLINE) | NEWLINE

if_stmt: "if" expr "then" block ["else" block] "endif"
for_stmt: "for" VAR_NAME "in" expr "do" block "endfor"
block: statement+

// --- 論理演算・比較演算の階層 ---
// 下に行くほど結合が強くなる

?expr: logical_or

?logical_or: logical_and
           | logical_or OR_OP logical_and  -> or_op
OR_OP: "OR"

?logical_and: logical_not
            | logical_and AND_OP logical_not -> and_and
AND_OP: "AND"

?logical_not: NOT_OP logical_not -> not_op
            | comparison
NOT_OP: "NOT"

?comparison: arrow COMPARISON_OP arrow -> compare_op
           | arrow

COMPARISON_OP: ">" | "<" | "==" | "!="

// --- 算術演算 ---
?arrow: sum | arrow "->" sum -> arrow_op
?sum: product | sum "+" product -> add | sum "-" product -> sub
?product: power | product "*" power -> mul | product "/" power -> div
?power: atom | atom "^" power -> pow

// ------------------------------
?atom: SIGNED_NUMBER    -> number
     | STRING           -> string_literal
     | VAR_NAME         -> var_load
     | BODY_NAME        -> body_load
     | funccall
     | auxcall
     | "(" expr ")"     // ここを expr にすることで括弧内の論理演算を許可

     | dot_access

//
assignment: VAR_NAME "=" expr  -> assign_var
          | BODY_NAME "=" expr  -> assign_body

dot_access: VAR_NAME "." VAR_NAME
          | dot_access "." VAR_NAME

funccall: (BODY_NAME | VAR_NAME) "(" [arglist] ")"
auxcall: (BODY_NAME | VAR_NAME) "[" [arglist] "]"
arglist: expr ("," expr)*

VAR_NAME: /[a-z][a-zA-Z0-9_]*/
BODY_NAME: /[A-Z][a-zA-Z0-9_]*/
STRING: /"[^"]*"/ | /'[^']*'/
COMMENT: "//" /[^\n]/*

%import common.SIGNED_NUMBER

# 空白無視を「改行を含まない空白」に限定する
%import common.WS_INLINE
%ignore WS_INLINE

# 改行は明示的に扱う
%import common.NEWLINE
%ignore COMMENT

