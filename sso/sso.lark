start: statement*
?statement: (assignment | expr) (";" | NEWLINE) | NEWLINE

assignment: VAR_NAME "=" expr  -> assign_var
          | BODY_NAME "=" expr  -> assign_body

?expr: arrow
?arrow: sum | arrow "->" sum -> arrow_op
?sum: product | sum "+" product -> add | sum "-" product -> sub
?product: power | product "*" power -> mul | product "/" power -> div
?power: atom | atom "^" power -> pow

?atom: NUMBER           -> number
     | STRING           -> string_literal
     | VAR_NAME         -> var_load
     | BODY_NAME         -> body_load
     | funccall
     | "(" expr ")"

funccall: (BODY_NAME | VAR_NAME) "(" [arglist] ")"
arglist: expr ("," expr)*

VAR_NAME: /[a-z][a-zA-Z0-9_]*/
BODY_NAME: /[A-Z][a-zA-Z0-9_]*/
STRING: /"[^"]*"/ | /'[^']*'/
COMMENT: "//" /[^\n]/*

%import common.NUMBER
%import common.WS
%import common.NEWLINE
%ignore WS
%ignore COMMENT
