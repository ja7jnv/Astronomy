// ================================
// Lexer
// ================================

%import common.CNAME           -> NAME
%import common.SIGNED_NUMBER   -> NUMBER
%import common.WS_INLINE
%import common.NEWLINE

%ignore WS_INLINE

_CONTINUATION: /\\(\r?\n)+/
%ignore _CONTINUATION


// ================================
// Parser
// ================================

start: (statement end)*

end: NEWLINE
   | ";"

statement: assignment
         | expr        -> expr_statement

assignment: NAME "=" expr

?expr: arrow

?arrow: sum
      | arrow "->" sum   -> arrow

?sum: term
    | sum "+" term     -> add
    | sum "-" term     -> sub

?term: factor
     | term "*" factor  -> mul
     | term "/" factor  -> div

?factor: constructor
       | var
       | NUMBER        -> number
       | "(" expr ")"

constructor: NAME "(" [expr ("," expr)*] ")"

var: NAME

